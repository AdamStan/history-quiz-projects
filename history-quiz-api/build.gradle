plugins {
    id 'java'
    id "org.openapi.generator" version "6.6.0"
    id "maven-publish"
}

group project.group
version = '0.0.3'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "io.springfox:springfox-swagger2:3.0.0"
    compileOnly "io.swagger.core.v3:swagger-annotations:2.2.8"
    compileOnly "org.springframework:spring-web:6.0.6"
    compileOnly "javax.servlet:servlet-api:2.5"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation "javax.validation:validation-api:2.0.1.Final"
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def yamlFile = "$projectDir/spec/api-schema.yaml".toString()
def openApiGeneratedClassesPath = "$buildDir/generated/src/main/java".toString()

sourceSets.main.java.srcDir openApiGeneratedClassesPath

openApiValidate {
    inputSpec = yamlFile
    recommend = true
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = tasks.openApiValidate.inputSpec
    outputDir = "$buildDir/generated".toString()

    apiPackage = "com.adam.stan.history.quiz.api.v1"
    modelPackage = "com.adam.stan.history.quiz.api.v1.model"

    configOptions = [
            artifactDescription : "description",
            artifactId: project.name,
            exceptionHandler: "false",
            generateBuilders: "true",
            gradleBuildFile: "false",
            interfaceOnly: "true",
            skipDefaultInterface: "true",
            openApiNullable: "false",
            useTags: "true"
    ]
}

tasks.openApiGenerate.dependsOn tasks.openApiValidate
tasks.compileJava.dependsOn tasks.openApiGenerate


tasks.register('printProjectName') {
    println ">> " + project.name
    println ">> " + project.version
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.adam.stan'
            artifactId project.name
            version project.version
            from components.java
        }
    }
}